# this function is executed once for every new user chatting with Dent
outputmacro: dent()  # The dent bot for java
$cs_token = #DO_INTERJECTION_SPLITTING | #DO_NUMBER_MERGE | #DO_DATE_MERGE  | #DO_PROPERNAME_MERGE | #DO_PARSE 

^addtopic(~tsaludos)
$cs_control_main = ~control
$cs_control_post = ~XPOSTPROCESS
$userprompt = ^"%user: >"
$botprompt = ^"Dent: "

table: defaultbot (^name)
^createfact(^name defaultbot defaultbot)
DATA:
dent

topic: ~control system ()
# on startup, do introduction
u: ( %input<%userfirstline) 
    gambit(~tsaludos)

u: (< shut up >) $shutup = 1
u: (< talk  >) $shutup = null

u: () # main per-sentence processing

    $$currenttopic = %topic     # get the current topic at start of volley

    if ( %response == 0 ) {nofail(TOPIC ^rejoinder())}  # try for rejoinders. might generate an answer directly from what we are looking for.
    
    # Check if it is a java question
    if ( %response == 0) {nofail(TOPIC ^respond(~JAVA))}
    if ( %response == 0) {nofail(TOPIC ^respond(~EJEMPLOS))}
    

    if (%length == 0 AND %response == 0 )
    {
        nofail(TOPIC ^gambit($$currenttopic))  # gambit current topic since no input (usually start of conversation)
    }

    if (%response == 0) { nofail(TOPIC ^respond($$currenttopic)) } # current topic tries to respond to his input

    if (%response == 0) # see if some other topic has keywords matching his input (given we have no response yet)
    {
        @8 = ^keywordtopics()   # get topics referred in input
        loop()
        {
            $$topic = first(@8subject)
            nofail(TOPIC ^respond($$topic))
            if (%response != 0) # stop when we find something to say
            {
                ^end(RULE)  # we are done, this terminates the loop (not the rule)
            }
        }
    }   
    
    # if we have rejoinders for what we said OR we asked a question, stop here
    # if (%outputrejoinder OR %lastquestion) 
    if (%outputrejoinder)
    {
        end(TOPIC)
    }

    if (%response == 0 AND ^marked($$currenttopic)) { nofail(TOPIC ^gambit($$currenttopic)) } # gambit current topic since keywords match current topic

    # if (%response == 0) # gambit from ANY matching topic
    # {
    #    @8 = ^keywordtopics()   # get topics referred in input
    #    loop()
    #    {
    #        $$topic = first(@8subject)
    #        nofail(TOPIC ^Gambit($$topic))  # gambit in best matching topic
    #        if (%response != 0) # stop when we find something
    #        {
    #            ^end(RULE)
    #        }
    #    }
    # }
    
        
    # if no topic reacts, go to the TSALUDOS keyworldless topic
    if (%response == 0) 
    {
        nofail(TOPIC ^respond(~TSALUDOS))
    }
    
    if (%response == 0){ nofail(TOPIC ^gambit($$currenttopic)) } # gambit from current topic even though no  keywords matched

    
    if (%response == 0) 
    {
         ^repeat() 
		 [Lo siento, no te he entendido. Podrias reformularlo, ¿por favor?]
		 [Perdona, no te he entendido bien. ¿Decías?]
		 [¿Eins? ¿Podrías repetir eso último?]
	}

topic: ~XPOSTPROCESS system () # gambits only - not allowed to write to  output - generates speech on windows -- for Linux you'd want to install Festival (or equivalent) and call that instead.

t: (^query(direct_v ? chatoutput ? -1 ? @9  ))  # get the sentences  (why is the object) -- no longer has quotes around it
    loop()
    {
        $$tmp = ^last(@9subject) # note last output which is quoted string
        if (!%server AND %os == windows  AND !$SHUTUP)
        {
            $$tmp = ^"talk \"$$tmp\" "
            popen($$tmp null)
        }
    }
